plugins {
    id 'java'
    id 'application'
}

group = 'com.github.krystianmuchla'

repositories {
    mavenCentral()
}

dependencies {
    final jettyVersion = '11.0.19'
    implementation "org.eclipse.jetty:jetty-server:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-servlet:$jettyVersion"
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    final jacksonVersion = '2.16.1'
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"

    implementation 'com.mysql:mysql-connector-j:8.2.0'

    implementation 'com.j2html:j2html:1.6.0'

    implementation 'ch.qos.logback:logback-classic:1.5.3'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.assertj:assertj-core:3.25.1'

    final testContainersVersion = '1.19.3'
    testImplementation "org.testcontainers:junit-jupiter:$testContainersVersion"
    testImplementation "org.testcontainers:mysql:$testContainersVersion"
}

application {
    final driveLocation = projectDir.toPath().resolve('home-local').resolve('drive').toString()
    applicationDefaultJvmArgs = [
        '-Ddatabase.url=jdbc:mysql://localhost:3306/home',
        '-Ddatabase.user=root',
        '-Ddatabase.password=root',
        '-Dnote-grave-cleaner.enabled=false',
        "-Ddrive.location=$driveLocation"
    ]
    mainClass = 'com.github.krystianmuchla.home.App'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
            'Main-Class': 'com.github.krystianmuchla.home.App'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}