plugins {
    id 'java'
    id 'application'
    id 'io.freefair.lombok' version '8.4'
}

group = 'com.github.krystianmuchla'
version = '1.0.3'

repositories {
    mavenCentral()
}

dependencies {
    final jettyVersion = '11.0.19'
    implementation "org.eclipse.jetty:jetty-server:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-servlet:$jettyVersion"
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    final jacksonVersion = '2.16.1'
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"

    implementation 'com.mysql:mysql-connector-j:8.2.0'
    implementation 'org.liquibase:liquibase-core:4.25.1'

    implementation 'org.slf4j:slf4j-simple:2.0.10'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation 'org.assertj:assertj-core:3.25.1'

    final testContainersVersion = '1.19.3'
    testImplementation "org.testcontainers:junit-jupiter:$testContainersVersion"
    testImplementation "org.testcontainers:mysql:$testContainersVersion"
}

application {
    applicationDefaultJvmArgs = [
        '-Dport=8080',
        '-Ddatabase.url=jdbc:mysql://localhost:3307/home',
        '-Ddatabase.user=root',
        '-Ddatabase.password=root',
        '-Dnote-grave-cleaner.enabled=true',
        '-Dnote-grave-cleaner.rate=10',
        '-Dnote-grave-cleaner.rate-unit=SECONDS',
        '-Dnote-grave-cleaner.threshold=1',
        '-Dnote-grave-cleaner.threshold-unit=MINUTES'
    ]
    mainClass = 'com.github.krystianmuchla.home.App'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
            'Main-Class': 'com.github.krystianmuchla.home.App'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}